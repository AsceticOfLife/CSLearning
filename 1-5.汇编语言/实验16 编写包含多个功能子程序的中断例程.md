# 编写包含多个子功能的中断例程

最开始的思路：将中断例程在安装程序的段中定义，结果发现并不能正确程序转移

```
assume cs:code,ds:

code segment
start:	;transmit
	mov ax,cs
	mov ds,ax
	mov si,offset setscreen
	
	mov ax,0
	mov es,ax
	mov di,200H
	
	mov cx,offset setend-offset setscreen
	cld
	rep movsb

	;set int7ch to new
	cli
	mov word ptr es:[7cH*4],200H
	mov word ptr es:[7cH*4+2],0
	sti	

	mov ax,4c00H
	int 21H

setscreen:
	jmp short set
	table dw sub1,sub2,sub3,sub4
set:	push bx

	cmp ah,3
	ja sret
	mov bl,ah
	mov bh,0
	add bx,bx
	
	call word ptr table[bx]

sret:	pop bx
	iret

sub1:	push ax
	push bx
	push cx
	push di
	push es
	
	mov ax,0b800H
	mov es,ax
	mov di,0
	mov cx,2000
sub1s:	mov byte ptr es:[di],' '
	add di,2
	loop sub1s

	pop es
	pop di
	pop cx
	pop bx
	pop ax
	ret

sub2:	push bx
	push cx
	push di
	push es
	
	mov bx,0b800H
	mov es,bx
	mov di,1
	mov cx,2000
sub2s:	and byte ptr es:[di],11111000B
	or es:[di],al
	add di,2
	loop sub2s

	pop es
	pop di
	pop cx
	pop bx
	ret

sub3:	push bx
	push cx
	push di
	push es
	
	mov bx,0b800H
	mov es,bx
	mov di,1
	mov cx,2000
sub3s:	and byte ptr es:[di],10001111B
	or es:[di],al
	add di,2
	loop sub3s

	pop es
	pop di
	pop cx
	pop bx
	ret

sub4:	push cx
	push si
	push di
	push es
	push ds

	mov si,0b800H
	mov es,si
	mov ds,si
	mov si,160
	mov di,0
	cld
	mov cx,24
sub4s:	push cx

	mov cx,160
	rep movsb

	pop cx
	loop sub4s

	mov cx,80
	mov si,0
sub4s1:	mov byte ptr es:[160*24+si],' '
	add si,2
	loop sub4s1

	pop ds
	pop es
	pop di
	pop si
	pop cx
	ret

setend:nop
code ends
end start
```

关键是这条语句：call word ptr table[bx]

这条语句的含义是：跳转到cs:table标记+bx处执行。table+bx应该是table处的偏移量（初始偏移量为0）

![image-20230715113624897](.\lab-image\16-1-1.png)

![image-20230715113653211](.\lab-image\16-1-2.png)

可以看到翻译的这条语句的机器指令是调用 CS：[BX+002C]处的指令，这个002C就是table标记的位置。

**此时CS寄存器的内容是0B37.**

将这段程序安装到0000:0200H处，再次查看内容：

![image-20230715114627195](.\lab-image\16-1-3.png)

从JMP指令开始，注意这条指令保存的是偏移量，E8 08，说明向下偏移8个字节。

![image-20230715114759883](.\lab-image\16-1-4.png)

**此时CS寄存器的内容是0，**BX寄存的内容也是0，因此这条指令会以0000:002cH内存单元保存的内容为IP寄存器内容，

![image-20230715115201779](.\lab-image\16-1-5.png)

也就是0054作为IP寄存器的内容继续执行：

![image-20230715115331407](.\lab-image\16-1-6.png)

可以看到程序跳转到0000:0054处执行，此时已经不是程序想要的跳转到子程序处执行。



解决方案，显式修改寄存器CS内容为0200H：

无法直接修改CS寄存器内容。这也是合理的，因为避免用户程序直接修改CS寄存器转移到并不属于自己的进程范围进行执行指令。



解决方案：采用不依赖于CS寄存器的数据标号，即采用判断然后转到相应的sub子程序中。

```
	cmp ah,0
	je do1
	cmp ah,1
	je do2
	cmp ah,2
	je do3
	cmp ah,3
	je do4

do1:	call sub1
	iret
do2:	call sub2
	iret
do3:	call sub3
	iret
do4:	call sub4
	iret
```



测试清屏：
![image-20230715152956229](.\lab-image\16-1-7.png)

测试设置前景色为1：

![image-20230715153206489](.\lab-image\16-1-8.png)

测试设置背景色为4：

![image-20230715153937992](.\lab-image\16-1-9.png)

说明最后一行左边是程序执行完毕之后才由DOS写上去的。



测试向上移动一行：

![image-20230715154053950](.\lab-image\16-1-10.png)

six4下面多出一行，说明向下移动。
