# 课程设计1

<img src=".\lab-image\10-4-1.png" alt="image-20230704114824139" style="zoom:80%;" />

<img src=".\lab-image\10-4-2.png" alt="image-20230704114927479" style="zoom:80%;" />

## 根据实验7得出的求最终结果的部分代码

```c++
assume cs:main
data segment
	db '1975','1976','1977','1978','1979','1980','1981','1982','1983'
	db '1984','1985','1986','1987','1988','1989','1990','1991','1992'
	db '1993','1994','1995'

	dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514
	dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000

	dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226
	dw 11542,14430,15257,17800
data ends

table segment
	db 21 dup ('year summ ne ?? ')
table ends
    
main segment
start: 	mov ax,data
	mov ds,ax
	mov ax,table
	mov es,ax

	mov bx,0
	mov di,0
	mov si,0
	mov cx,21
s:	mov dx,ds:0[di]
	mov es:[bx+0],dx
	mov ax,ds:84[di]
	mov es:[bx+5],ax

	mov dx,ds:168[si]
	mov es:[bx+10],dx

	add di,2
	mov dx,ds:0[di]
	mov es:[bx+2],dx
	mov dx,ds:84[di]
	mov es:[bx+7],dx

	div word ptr es:[bx+10]
	mov es:[bx+13],ax

	add di,2
	add si,2
	add bx,16
	loop s
```

这一部分代码是处理数据得到结果数据，最终结果如下图所示：
![image-20230629120939372](.\lab-image\7-1-7.png)

## 打印空白子程序

**show_str3**该函数接收三个参数，DH寄存器保存行数，DL寄存器保存列数，SI寄存器表示打印次数，主要功能是从DH行DL列开始打印SI个空格。这里的行列下标从0开始计数。

```c++
show_str3:
	mov ax,0B800H
	mov es,ax
	mov al,0A0H
	mul dh
	mov di,0
	add di,ax
	mov al,2
	mul dl
	add di,ax

move3:	mov cx,si
	jcxz ok4
	mov byte ptr es:[di],20H
	mov byte ptr es:[di+1],07H
	add di,2
	dec si
	jmp short move3

ok4:	ret
```

## 打印字符直到遇到20H

**show_str**函数参数有5个，首先DH寄存器表示行数，DL寄存器表示列数，DS寄存器表示源段地址，SI表示源段地址偏移，CL寄存器表示字符属性，主要功能是从源段地址DS的指定偏移位置SI开始，打印字符，直到遇到20H也就是空格停止。

```c++
show_str:
	mov ax,0B800H
	mov es,ax
	mov al,0A0H
	mul dh
	mov di,0
	add di,ax
	mov al,2
	mul dl
	add di,ax
	mov al,cl
move1:	mov cl,ds:[si]
	mov ch,0
	sub cl,20H
	jcxz ok1
	mov cl,ds:[si]
	mov es:[di],cl
	mov es:[di+1],al
	inc si
	add di,2
	jmp short move1
ok1:	ret
```

## 将数值转换成字符dtoc

**dtoc**函数的参数为：操作数据：AX存放低16位，DX存放高16位。ES寄存器表示转换成的字符存在的位置，DI寄存器表示偏移地址。函数执行结束之后，数值转换成的字符存放在ES段位置，DI表示字符的下一个位置，因为从0开始，所以DI也表示字符个数。

```c++
dtoc:	mov cx,10
	call divdw
	mov bx,cx
	mov cx,ax
	jcxz ok3
	add bl,30H
	mov es:[di],bl
	inc di
	jmp short dtoc

ok3:	add bl,30H
	mov es:[di],bl
	inc di
	ret
```

处理逻辑是调用divdw函数进行32位除法，保证不会发生溢出，然后将CX中的余数移动到ES段位置。不断除法的终止条件是AX（商的低16位）为0。

**divdw**函数参数：AX存放被除数低16位，DX存放被除数高16位，CX寄存器存放除数（这里是10）。结果：AX存放商的低16位，DX存放商的高16位，CX存放余数。

```c++
divdw:	push ax
	mov ax,dx
	mov dx,0
	div cx
	
	pop bx
	push ax
	mov ax,bx
	div cx

	mov cx,dx
	pop dx
	ret
```

## 逆序打印数值转化的字符

**show_str2**函数参数：DH寄存器存放行数，DL寄存器存放列数，CL寄存器存放字符属性，SI表示最后一个字符后面的偏移地址（由于下标从0开始，所以也可以理解为字符个数），以及DS寄存器保存源数据段地址。程序主要功能是逆序打印SI个字符。

```c++
show_str2:
	mov ax,0B800H
	mov es,ax
	mov al,0A0H
	mul dh
	mov di,0
	add di,ax
	mov al,2
	mul dl
	add di,ax
	mov al,cl	

move2:	mov cx,si
	jcxz ok2
	mov ah,ds:[si-1]
	mov es:[di],ah
	mov es:[di+1],al
	dec si
	add di,2
	jmp short move2	

ok2:	ret
```



## 整体大循环

需要循环21次打印21行数据，DH表示行数，CX表示循环次数，由于循环中的函数会使用到DX和CX寄存器，从而改变其中的值，所以循环开始时需要将两个寄存器入栈。结束时再出栈。

由于整个循环太长，不能使用loop进行短转移，因此使用jcxz和jmp far ptr进行长转移。

<img src=".\lab-image\10-4-3.png" alt="image-20230706092514992" style="zoom:80%;" />

最后需要修改读取数据的偏移，根据行数进行计算每一行要处理的数据的偏移位置。

比如要处理第1行的第一个数据，偏移量就是0+（行数-1）*16（一行的字节数）

```c++
	mov si,0
	mov al,dh
	mov ah,0
	sub al,1
	mov bl,16
	mul bl
	add si,ax
```

# 实验结果

![image-20230706093542586](.\lab-image\10-4-4.png)

# 全部代码

```
assume cs:main

data segment
	db '1975','1976','1977','1978','1979','1980','1981','1982','1983'
	db '1984','1985','1986','1987','1988','1989','1990','1991','1992'
	db '1993','1994','1995'

	dd 16,22,382,1356,2390,8000,16000,24486,50065,97479,140417,197514
	dd 345980,590827,803530,1183000,1843000,2759000,3753000,4649000,5937000

	dw 3,7,9,13,28,38,130,220,476,778,1001,1442,2258,2793,4037,5635,8226
	dw 11542,14430,15257,17800
data ends

table segment
	db 21 dup ('year summ ne ?? ')
table ends

temp segment
	db 10 dup(0)
temp ends

main segment
start: 	mov ax,data
	mov ds,ax
	mov ax,table
	mov es,ax

	mov bx,0
	mov di,0
	mov si,0
	mov cx,21
s:	mov dx,ds:0[di]
	mov es:[bx+0],dx
	mov ax,ds:84[di]
	mov es:[bx+5],ax

	mov dx,ds:168[si]
	mov es:[bx+10],dx

	add di,2
	mov dx,ds:0[di]
	mov es:[bx+2],dx
	mov dx,ds:84[di]
	mov es:[bx+7],dx

	div word ptr es:[bx+10]
	mov es:[bx+13],ax

	add di,2
	add si,2
	add bx,16
	loop s
	
	mov dh,1
	mov dl,0
	mov cx,21
mainloop:
	push cx
	push dx
	mov dl,0
	mov si,1
	call show_str3
	
	pop dx
	push dx
	mov dl,1
	mov cl,2
	mov ax,table
	mov ds,ax
	mov si,0
	mov al,dh
	mov ah,0
	sub al,1
	mov bl,16
	mul bl
	add si,ax
	call show_str
	
	pop dx
	push dx
	mov dl,5
	mov si,5
	call show_str3	

	pop dx
	push dx
	mov al,dh
	mov ah,0
	sub al,1
	mov bl,16
	mul bl
	mov si,5
	add si,ax
	mov ax,table
	mov ds,ax
	mov ax,ds:[si]
	mov dx,ds:[si+2]
	mov bx,temp
	mov es,bx
	mov di,0
	call dtoc
	mov ax,temp
	mov ds,ax
	pop dx
	push dx
	mov dl,10
	mov cl,2
	mov si,di
	push di
	call show_str2
	
	pop ax
	pop dx
	push dx
	mov dl,10
	add dl,al
	mov si,10
	sub si,ax
	call show_str3

	pop dx
	push dx
	mov dl,20
	mov si,5
	call show_str3

	pop dx
	push dx
	mov al,dh
	mov ah,0
	sub al,1
	mov bl,16
	mul bl
	mov si,10
	add si,ax
	mov ax,table
	mov ds,ax
	mov ax,ds:[si]
	mov dx,0
	mov bx,temp
	mov es,bx
	mov di,0
	call dtoc
	mov ax,temp
	mov ds,ax
	pop dx
	push dx
	mov dl,25
	mov cl,2
	mov si,di
	push di
	call show_str2
	
	pop ax
	pop dx
	push dx	
	mov dl,25
	add dl,al
	mov si,5
	sub si,ax
	call show_str3

	pop dx
	push dx
	mov dl,30
	mov si,5
	call show_str3
	
	pop dx
	push dx
	mov al,dh
	mov ah,0
	sub al,1
	mov bl,16
	mul bl	
	mov si,13
	add si,ax
	mov ax,table
	mov ds,ax
	mov ax,ds:[si]
	mov dx,0
	mov bx,temp
	mov es,bx
	mov di,0
	call dtoc
	mov ax,temp
	mov ds,ax
	pop dx
	push dx
	mov dl,35
	mov cl,2
	mov si,di
	push di
	call show_str2

	pop ax
	pop dx
	push dx
	mov dl,35
	add dl,al
	mov si,5
	sub si,ax
	call show_str3

	pop dx
	push dx
	mov dl,40
	mov si,40
	call show_str3
	
	pop dx
	pop cx
	add dh,1
	sub cx,1
	jcxz F
	jmp far ptr mainloop

F:	mov ax,4c00H
	int 21H

show_str:
	mov ax,0B800H
	mov es,ax
	mov al,0A0H
	mul dh
	mov di,0
	add di,ax
	mov al,2
	mul dl
	add di,ax
	mov al,cl
move1:	mov cl,ds:[si]
	mov ch,0
	sub cl,20H
	jcxz ok1
	mov cl,ds:[si]
	mov es:[di],cl
	mov es:[di+1],al
	inc si
	add di,2
	jmp short move1
ok1:	ret

dtoc:	mov cx,10
	call divdw
	mov bx,cx
	mov cx,ax
	jcxz ok3
	add bl,30H
	mov es:[di],bl
	inc di
	jmp short dtoc

ok3:	add bl,30H
	mov es:[di],bl
	inc di
	ret
	
divdw:	push ax
	mov ax,dx
	mov dx,0
	div cx
	
	pop bx
	push ax
	mov ax,bx
	div cx

	mov cx,dx
	pop dx
	ret

show_str2:
	mov ax,0B800H
	mov es,ax
	mov al,0A0H
	mul dh
	mov di,0
	add di,ax
	mov al,2
	mul dl
	add di,ax
	mov al,cl	

move2:	mov cx,si
	jcxz ok2
	mov ah,ds:[si-1]
	mov es:[di],ah
	mov es:[di+1],al
	dec si
	add di,2
	jmp short move2	

ok2:	ret

show_str3:
	mov ax,0B800H
	mov es,ax
	mov al,0A0H
	mul dh
	mov di,0
	add di,ax
	mov al,2
	mul dl
	add di,ax

move3:	mov cx,si
	jcxz ok4
	mov byte ptr es:[di],20H
	mov byte ptr es:[di+1],07H
	add di,2
	dec si
	jmp short move3

ok4:	ret

main ends

end start


	
```

# 设计总结

## 代码逻辑总结

首先是实验7中的处理数据，主要用到的知识就是内存地址的访问，主要是以数组的视角去看待内存单元的排列。

接着是一个整体大循环21次，每次循环处理一行。
1.首先处理第一个数据，年份。这个数据已经是字符，所以可以直接打印；
2.接着处理第二个数据，这个数据是数值，所以需要先经过转换成字符，然后再进行打印；
3.后面两个数据处理过程与第二个数据类似。

## 设计总结

主要用到的知识点：

1.内存单元的访问

2.乘法、除法指令

3.向显存单元写入字符和属性

4.跳转指令

5.call与ret指令组合成子程序





















