# 实验15 安装新的int9中断例程

## 任务1

<img src=".\lab-image\15-1-1.png" alt="image-20230714093426859" style="zoom:67%;" />

```
assume cs:code

stack segment
	db 128 dup (0)
stack ends

code segment
start:	mov ax,stack
	mov ss,ax
	mov sp,128

	push cs
	pop ds
	mov ax,0
	mov es,ax
	mov si,offset int9
	mov di,204H
	mov cx,offset int9end-offset int9
	cld 
	rep movsb

	;save original int9
	push es:[9*4]
	pop es:[200H]
	push es:[9*4+2]
	pop es:[202H]

	;set new int9
	cli
	mov word ptr es:[9*4],204H
	mov word ptr es:[9*4+2],0
	sti
	
	mov ax,4c00H
	int 21H

int9:	push ax
	push cx
	push di
	push es
	
	in al,60H
	
	;call original int9
	pushf
	call dword ptr cs:[200H]
	
	cmp al,9eH
	jne int9ret

	mov ax,0b800H
	mov es,ax
	mov di,0
	mov cx,2000
s:	mov byte ptr es:[di],'A'
	add di,2
	loop s
		
int9ret:
	pop es
	pop di
	pop cx
	pop ax
	iret
int9end:nop
	
code ends
end start
```

没法进入实模式，无法测试代码。

