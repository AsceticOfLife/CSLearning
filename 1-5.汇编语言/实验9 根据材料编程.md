# 实验9 根据材料编程

## 任务

在屏幕中间分别显示绿色、绿底红色、白底蓝色的字符串“welcome to masm”

设置窗口大小为80*25，80表示一行有80个字符，25表示有25行。
![image-20230701174813352](.\lab-image\9-1-1.png)

由于是三行，手动计算出需要修改的显存的偏移地址为：6E0H到77FH（第12行）、780H到81FH（第13行）、820H到8BFH（第14行），由于一共是16个字符，每个字符占用2个字节，计算出起始字符偏移地址为起始行偏移地址+30H。

### 第一个版本

采用三个循环完成任务：

```
assume cs:code

data segment
	db 'welcome to masm!'
data ends

code segment
start:	mov ax,data
	mov ds,ax

	mov ax,0B800H
	mov es,ax
	
	mov bx,0
	mov si,6E0H
	add si,30H
	mov cx,16
s1:	mov al,ds:[bx]
	mov es:[si],al
	mov byte ptr es:[si+1],02H

	inc bx
	add si,2
	loop s1
	
	mov bx,0
	mov si,780H
	add si,30H
	mov cx,16
s2:	mov al,ds:[bx]
	mov es:[si],al
	mov byte ptr es:[si+1],24H

	inc bx
	add si,2
	loop s2

	mov bx,0
	mov si,820H
	add si,30H
	mov cx,16
s3:	mov al,ds:[bx]
	mov es:[si],al
	mov byte ptr es:[si+1],71H

	inc bx
	add si,2
	loop s3	
		
	mov ax,4c00H
	int 21H
code ends

end start
	
	
```

执行程序结果如下：
![image-20230701175234369](.\lab-image\9-1-2.png)

至于为什么从第11行开始显示，猜想是执行n4.exe时修改的是第12行，但是执行完毕之后命令行会自动换行，导致整体向上移动一行。程序执行过程是，当输入n4.exe之后加一个换行符，此时开始执行程序，所以第25行应该是n4.exe后面的空行。从空行开始数，welcome这一行确实是在第12行。

## 第二个版本

观察上面三个循环，每次变化的其实就是每一行的起始地址，而每一行的增量是固定的。所以尝试用一个外层循环处理三次行输入。
另外，由于三次的“属性值不同，可以使用一块内存预先保存这三个值，但是寄存器数量有限，所以使用内存保存这三个数据。

```
assume cs:code

data segment
	db 'welcome to masm!'
data ends

stack segment
	dw 02H
	dw 24H
	dw 71H
	dw 0
stack ends

code segment
start:	mov ax,data
	mov ds,ax
	mov ax,stack
	mov ss,ax
	mov ax,0B800H	
	mov es,ax

	mov dx,6E0H
	add dx,30H
	mov si,0
	mov cx,3
outer:	mov di,0
	add di,dx
	mov bx,0
	mov ss:[6],cx
	mov cx,16

inner:	mov ah,0
	mov al,ds:[bx]
	mov es:[di],al
	mov al,ss:[si]
	mov es:[di+1],al

	inc bx
	add di,2
	loop inner
	
	add dx,0A0H
	add si,2
	mov cx,ss:[6]
	loop outer

	mov ax,4c00H
	int 21H
code ends

end start
```

程序载入之后：

<img src=".\lab-image\9-1-3.png" alt="image-20230701211806488" style="zoom:67%;" />

程序执行完毕之后：
![image-20230702094552433](.\lab-image\9-1-4.png)







